#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_scalar_block_layout : require

#define RAY_TRACING
#include "raytracing.glsl"

layout(location = 0) rayPayloadEXT RayPayload payload;

void main()
{
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
    vec2 d = inUV * 2.0 - 1.0;
    vec4 origin = inverse(ubo.view) * vec4(0, 0, 0, 1);
    vec4 target = inverse(ubo.proj) * vec4(d.x, d.y, 1, 1);
    vec4 direction = inverse(ubo.view) * vec4(normalize(target.xyz / target.w), 0);
    vec3 ray_origin = origin.xyz;
    vec3 ray_dir = normalize(direction.xyz);

    vec3 accumulated_color = vec3(0.0);
    vec3 throughput = vec3(1.0);

    for (int depth = 0; depth < 8; depth++) { 
        traceRayEXT(tlas, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, ray_origin, 0.001, ray_dir, 10000.0, 0);

        accumulated_color += payload.color * throughput;

        if (payload.hit_flag < 1.5) break; 

        throughput *= payload.weight;
        ray_origin = payload.next_ray_origin;
        ray_dir = payload.next_ray_dir;
        
        if (length(throughput) < 0.01) break;
    }

    vec3 finalColor = pow(accumulated_color, vec3(1.0 / 2.2));
    imageStore(rt_output_image, ivec2(gl_LaunchIDEXT.xy), vec4(finalColor, 1.0));
}